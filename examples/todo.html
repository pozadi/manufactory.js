<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8">
  <title>ToDo (manufactory.js example)</title>
  <script src="http://code.jquery.com/jquery-1.8.3.min.js"></script>
  <script src="http://cdnjs.cloudflare.com/ajax/libs/underscore.js/1.4.2/underscore-min.js"></script>
  <script src="../manufactory.js"></script>
  <script src="http://coffeescript.org/extras/coffee-script.js"></script>

  <style type="text/css">
    .todos-widget {
      width: 500px;
      margin: 0 auto;
      font-size: 20px;
      line-height: 1.5;
    }
      .item-list {
        list-style: none;
        margin: 0;
        padding: 0;
      }
        .todo-item,
        .add-form {
          background: #8ee699;
          padding: 4px 6px;
        }
          .todo-item [type=text],
          .add-form [type=text] {
            width: 90%;
            font-size: 20px;
            padding: 5px 7px;
          }
            .remove {
              float: right;
            }
            .todo-item [type=text] {
              display: none;
            }
            .todo-item.edit .remove,
            .todo-item.edit .text,
            .todo-item.edit [type=checkbox] {
              display: none;
            }
            .todo-item.edit [type=text] {
              display: inline;
            }
            .todo-item.completed .text {
              text-decoration: line-through;
            }
          .todos-widget.filter-completed .todo-item.active {
            display: none;
          }
          .todos-widget.filter-active .todo-item.completed {
            display: none;
          }
      .footer {
        background: #63c26f;
        margin: 0 4px;
        font-size: 14px;
        padding: 5px 7px;
      }
        .left {
          float: left;
        }
        .clear-completed {
          float: right;
        }
        .footer-menu {
          list-style: none;
          margin: 0;
          padding: 0;
          text-align: center;
        }
          .footer-menu li {
            display: inline;
          }
            .todos-widget.filter-all [data-filter=all] {
              font-weight: bold;
            }
            .todos-widget.filter-completed [data-filter=completed] {
              font-weight: bold;
            }
            .todos-widget.filter-active [data-filter=active] {
              font-weight: bold;
            }
  </style>

  <script type="text/coffeescript">

    manufactory.module 'Todos', (M) ->

      onEnter = (fn) ->
        (_, e) -> fn.apply(@, arguments) if e.which == 13

      M.tree """
        .js-todos-widget
          .js-all-checkbox
          .js-new-item
            .js-item-list
              .js-todo-item        / dynamic
                .js-item-checkbox  / dynamic
                .js-item-text      / dynamic
                .js-item-remove    / dynamic
                .js-item-field     / dynamic
          .js-footer
            .js-left-counter
            .js-clear-completed
              .js-completed-counter
            .js-set-filter-button
          .js-item-template
      """

      M.events """
        change    allCheckbox      _toggleAll
        keyup     newItem          _addItemOnEnter
        change    itemCheckbox     _toggleItem
        dblclick  itemText         _enterEditMode
        blur      itemField        _saveItem
        keyup     itemField        _saveItemOnEnter
        click     itemRemove       _removeItem
        click     clearCompleted   _clearCompleted
        click     setFilterButton  _setFilter
      """

      M.methods
        initializer: ->
          @itemTpl = @el.itemTemplate.html()
          @setFilter('all')
          @_updateStats()

        addItem: (text) ->
          todoItem = $(@itemTpl)
          @el.itemList.append(todoItem)
          @updateItem(todoItem, text)
          @setCompleted(todoItem, false)

        updateItem: (todoItem, text) ->
          @el.itemText.byParent(todoItem).text text
          @el.itemField.byParent(todoItem).val text

        setCompleted: (todoItem, completed) ->
          $(todoItem)
            .removeClass('active completed')
            .addClass(if completed then 'completed' else 'active')
          @el.itemCheckbox.byParent(todoItem).prop 'checked', completed
          @_updateStats()

        removeItem: (todoItem) ->
          todoItem.remove()
          @_updateStats()

        setFilter: (filter) ->
          @el.root
            .removeClass('filter-completed filter-active filter-all')
            .addClass("filter-#{filter}")

        _updateStats: ->
          [all, completed, active] = [
            @el.todoItem().length, 
            @el.todoItem('.completed').length, 
            all - completed
          ]
          @el.footer.toggle(all > 0)
          @el.clearCompleted.toggle(completed > 0)
          @el.completedCounter.text(completed)
          @el.leftCounter.text("#{active} #{if active == 1 then 'item' else 'items'} left")

        _toggleAll: ->
          completed = @el.allCheckbox.prop('checked')
          @setCompleted(todoItem, completed) for todoItem in @el.todoItem()

        _addItemOnEnter: onEnter ->
          @addItem @el.newItem.val()
          @el.newItem.val('')

        _toggleItem: (itemCheckbox) ->
          todoItem = @el.todoItem.byChild(itemCheckbox)
          @setCompleted(todoItem, $(itemCheckbox).prop 'checked')

        _enterEditMode: (itemText) ->
          todoItem = @el.todoItem.byChild(itemText)
          itemField = @el.itemField.byParent(todoItem)
          todoItem.addClass('edit')
          itemField.focus()

        _saveItem: (itemField) ->
          todoItem = @el.todoItem.byChild(itemField).removeClass('edit')
          @updateItem todoItem, $(itemField).val()

        _saveItemOnEnter: onEnter (itemField) ->
          @_saveItem(itemField)

        _removeItem: (itemRemove) ->
          @removeItem @el.todoItem.byChild(itemRemove)

        _clearCompleted: ->
          @removeItem(todoItem) for todoItem in @el.todoItem('.completed')

        _setFilter: (setFilterButton) ->
          @setFilter $(setFilterButton).data('filter')

  </script>

</head>
<body>
  
  <div class="js-todos-widget todos-widget">
    <h1>todos</h1>

    <div class="add-form">
      <input type="checkbox" class="js-all-checkbox">
      <input type="text" placeholder="What needs to be done?" class="js-new-item">
    </div>

    <ul class="js-item-list item-list">
    </ul>

    <div class="footer js-footer">
      <span class="js-left-counter left"></span>

      <button class="js-clear-completed clear-completed">Clear completed 
        (<span class="js-completed-counter"></span>)</button>

      <ul class="footer-menu">
        <li><button class="js-set-filter-button" data-filter="all">All</button></li>
        <li><button class="js-set-filter-button" data-filter="active">Active</button></li>
        <li><button class="js-set-filter-button" data-filter="completed">Completed</button></li>
      </ul>
    </div>

    <script type="text/html" class="js-item-template">
      <li class="js-todo-item todo-item">
        <input type="checkbox" class="js-item-checkbox">
        <span class="js-item-text text"></span>
        <button class="js-item-remove remove">&times;</button>
        <input type="text" class="js-item-field">
      </li>
    </script>
  </div>

</body>
</html>
